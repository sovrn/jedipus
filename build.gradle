buildscript { repositories { jcenter() } }
plugins {
  id 'java'
  id 'pl.allegro.tech.build.axion-release' version '1.4.1'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.7.3'
  id 'findbugs'
  id 'checkstyle'
}

scmVersion {
  tag {
    prefix = project.name
  }
  repository {
    if (project.hasProperty("AXION_RELEASE_GITHUB_TOKEN")) {
      customUsername = "$AXION_RELEASE_GITHUB_TOKEN"
    }
  }
  createReleaseCommit true
  releaseCommitMessage { version, position -> "Release $project.name-$version" }
  checks {
    uncommittedChanges = false
  }
}

project.group = 'com.fabahaba'
project.version = scmVersion.version

sourceCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
  testCompile 'junit:junit:+'
}

sourceSets {
  test {
    java {
      srcDir 'src/unit/java'
      srcDir 'src/integ/java'
    }
    resources {
      srcDir 'src/unit/resources'
      srcDir 'src/integ/resources'
    }
  }
}

test {
  outputs.upToDateWhen { false }
  testLogging {
    events "failed", "standardOut", "standardError"
    showExceptions true
    showStackTraces true
    exceptionFormat "full"
  }
  systemProperty 'jedipus.redis.port', '9736'
  systemProperty 'jedipus.redis.pass', '42'
  systemProperty 'jedipus.redis.ssl.port', '6443'
  systemProperty 'jedipus.redis.ssl.truststore.jceks', 'stunnel/integ.jks'

  systemProperty 'jedipus.redis.cluster.announceip', '127.0.0.1'
  systemProperty 'jedipus.redis.cluster.startingport', '7379'
  systemProperty 'jedipus.redis.cluster.nummasters', '3'
  systemProperty 'jedipus.redis.cluster.numslaveseach', '1'
}

task startRedis(type:Exec) {
  commandLine 'docker-compose', 'up', '-d'
}

task rmRedis(type:Exec) {
  commandLine 'docker-compose', 'down'
}

task getRedisModule() {
  OutputStream moduleHeaderFile = new File('./redis/redismodule.h').newOutputStream()
  moduleHeaderFile << new URL('https://raw.githubusercontent.com/antirez/redis/unstable/src/redismodule.h').openStream()
  moduleHeaderFile.close()
}

task compileIntegModules(type:Exec) {
  String dir = System.getProperty("user.dir");
  commandLine 'docker', 'run', '--rm', '-v', "$dir/redis/:/tmp", 'comodal/alpine-gcc-make:latest', '/bin/sh', '-c', 'make -B -C /tmp/modules'
}

task makeClean(type:Exec) {
  String dir = System.getProperty("user.dir");
  commandLine 'docker', 'run', '--rm', '-v', "$dir/redis/:/tmp", 'comodal/alpine-gcc-make:latest', '/bin/sh', '-c', 'make -C /tmp/modules clean'
}

compileIntegModules.dependsOn getRedisModule
startRedis.dependsOn compileIntegModules
test.dependsOn startRedis
test.finalizedBy rmRedis
rmRedis.finalizedBy makeClean

task sourcesJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

publishing {
  publications {
    mavenJava( MavenPublication ) {
      from components.java

      artifact sourcesJar
    }
  }
}

artifacts {
  archives sourcesJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_API_KEY')
  publications = ['mavenJava']
  pkg {
    repo = 'libs'
    name = project.name
    desc = 'Redis Java 8 Client & Executor'
    websiteUrl = 'https://github.com/jamespedwards42/' + project.name
    vcsUrl = 'https://github.com/jamespedwards42/' + project.name
    issueTrackerUrl = 'https://github.com/jamespedwards42/' + project.name + '/issues'
    licenses = ["Apache-2.0"]
    publish = true
    version {
       name = project.version
       vcsTag = scmVersion.tag.prefix + '-' + project.version
       gpg {
          sign = true
          passphrase = System.getenv('BINTRAY_GPG_PASSPHRASE')
       }
    }
  }
}

bintrayUpload.onlyIf { !project.version.endsWith( 'SNAPSHOT' ) }

findbugs {
  toolVersion = "+"
  reportsDir = file("$project.buildDir/findbugsReports")
  effort = "max"
  reportLevel = "low"
  ignoreFailures = false
  excludeFilter = file("findbugs-exclude.xml")
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

checkstyle {
  config = resources.text.fromFile("checkstyle/google_checks.xml")
  configProperties = [samedir: "${projectDir}/checkstyle"]
  reportsDir = file("$project.buildDir/checkstyleReports")
  toolVersion = "+"
  ignoreFailures = false
}

gradle.addListener(new TestEventLogger())

class TestEventLogger implements org.gradle.api.tasks.testing.TestListener {

  public void beforeSuite(TestDescriptor suite) {
    if(suite.getClassName() != null) {
      println 'Running ' + suite.getClassName() + ' test suite.'
    }
  }

  public void afterSuite(TestDescriptor suite, TestResult result) {
  }

  public void beforeTest(TestDescriptor test) {
    println '\n' + test.getName() + ' STARTED @ ' + new Date().toTimestamp()
  }

  public void afterTest(TestDescriptor test, TestResult result) {
    println test.getName() + ' ' + result.getResultType() + ' in ' + (result.getEndTime() - result.getStartTime()) + 'ms.'
  }
}
